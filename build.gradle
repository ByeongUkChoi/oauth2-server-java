buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java'
//apply plugin: 'maven'
apply plugin: 'maven-publish'

archivesBaseName = 'oauth2-server-java'
group = 'com.github.byeongukchoi'
version = '0.0.26'
description = 'OAuth2 server library'

repositories {
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The MIT License (MIT)"
            url "https://mit-license.org/"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "byeongukchoi"
            name "ByeongUkChoi"
            email "cbw0916@gmail.com"
        }
    }
    scm {
        url "https://github.com/ByeongUkChoi/oauth2-server-java.git"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library for implementing an OAuth2 Server in java')
                root.appendNode('name', 'oauth2-server-java')
                root.appendNode('url', 'https://github.com/ByeongUkChoi/oauth2-server-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.property('bintray.user')
    key = project.property('bintray.key')
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'library'
        name = 'oauth2-server-java'
        userOrg = 'cbw0916'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/ByeongUkChoi/oauth2-server-java.git'
        version {
            name = project.version
            desc = project.description
            released  = new Date()
            gpg {
                sign = true
                passphrase = project.property("bintray.gpgPassword")
            }
        }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    // testCompile -> 더이상 사용되지 않을 예정. 아래와 같이 사용해야함.
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.4.4'

    // https://mvnrepository.com/artifact/org.apache.maven.plugins/maven-javadoc-plugin
    compileOnly group: 'org.apache.maven.plugins', name: 'maven-javadoc-plugin', version: '3.2.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
}

test {
    useJUnitPlatform()
}